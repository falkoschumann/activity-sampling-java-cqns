import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id 'application'
  id 'org.beryx.jlink'
  id 'muspellheim.java-common-conventions'
}

jlink {
  options = [
    '--strip-native-commands',
    '--strip-debug',
    '--no-man-pages',
    '--no-header-files',
    '--compress', '2',
    '--include-locales', 'en,de',
  ]
  jpackage {
    imageOptions += [
      '--description', 'The application periodically asks the user for his current activity and creates a log of his activities per day.',
      '--vendor', 'Muspellheim',
    ]
    installerOptions = [
      '--license-file', "$rootDir/LICENSE.txt",
    ]
  }
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      icon = "$projectDir/src/main/mac/app-icon.icns"
      imageOptions += [
        '--copyright', "Copyright Â© ${copyrightYear} Falko Schumann",
        '--mac-sign',
        '--mac-signing-key-user-name', 'Falko Schumann (QC6EN37P56)',
        '--resource-dir', "$projectDir/src/main/mac",
      ]
      installerType = 'dmg'
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = "$projectDir/src/main/win/app-icon.ico"
      imageOptions = [
        '--copyright', "Copyright (c) ${copyrightYear} Falko Schumann",
        '--resource-dir', "$projectDir/src/main/win",
      ]
      installerType = 'msi'
      installerOptions = [
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'Muspellheim',
      ]
    }
  }
}
