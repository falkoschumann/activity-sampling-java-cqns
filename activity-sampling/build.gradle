import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id 'todomvc.java-application-conventions'
  id 'com.gluonhq.client-gradle-plugin'
  id 'org.beryx.jlink'
}

version = '1.0.0'

dependencies {
  implementation project(':activity-sampling-frontend')
  implementation project(':activity-sampling-backend')
}

application {
  mainModule = 'de.muspellheim.activitysampling'
  mainClassName = 'de.muspellheim.activitysampling.App'
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      imageName = 'Activity Sampling'
      icon = 'src/main/macos/AppIcon.icns'
      imageOptions = ["--mac-sign", "--mac-signing-key-user-name", project.findProperty("macSigning.keyUserName")]
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      imageName = 'activity-sampling'
      icon = 'src/main/win/app.ico'
    }
  }
}
