import org.apache.tools.ant.taskdefs.condition.Os

import java.time.LocalDate

plugins {
  id 'todomvc.java-application-conventions'
  id 'todomvc.java-openjfx-conventions'
  id 'com.gluonhq.client-gradle-plugin'
  id 'org.beryx.jlink'
}

version = '1.0.0'

def year = LocalDate.now().year

dependencies {
  implementation project(':activity-sampling-frontend')
  implementation project(':activity-sampling-backend')
}

application {
  mainModule = 'de.muspellheim.activitysampling'
  mainClass = 'de.muspellheim.activitysampling.App'
  applicationDefaultJvmArgs = [
    "-Dapp.version=${project.version}".toString(),
    "-Dapp.copyright=${year} Falko Schumann".toString()
  ]
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher{
    jvmArgs = application.applicationDefaultJvmArgs
  }
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      icon = 'src/main/macos/AppIcon.icns'
      imageName = 'Activity Sampling'
      installerType = 'dmg'
      imageOptions = [
        '--copyright', "Copyright (c) ${year} Falko Schumann",
        '--mac-sign',
        '--mac-signing-key-user-name', project.findProperty('macSigning.keyUserName')
      ]
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = 'src/main/win/app.ico'
      imageName = 'Activity Sampling'
      imageOptions = [
        '--copyright', "Copyright (c) ${year} Falko Schumann",
      ]
      installerName = 'Activity Sampling'
      installerType = 'msi'
      installerOptions = [
        '--copyright', "Copyright (c) ${year} Falko Schumann",
        '--license-file', '../LICENSE.txt',
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'Muspellheim',
        '--win-upgrade-uuid', 'cce3747c-06dc-466c-8c49-4aebe528da61'
      ]
    }
  }
}
