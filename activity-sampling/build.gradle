import org.apache.tools.ant.taskdefs.condition.Os

import java.time.LocalDate

plugins {
  id 'activity-sampling.java-application-conventions'
  id 'com.gluonhq.client-gradle-plugin'
  id 'org.beryx.jlink'
}

dependencies {
  implementation project(':activity-sampling-frontend')
  implementation project(':activity-sampling-backend')
}

def year = LocalDate.now().year
def copyright = "Copyright (c) ${year} Falko Schumann"

processResources {
  filesMatching('**/*.properties') {
    expand(title: 'Todos', version: rootProject.version, copyright: copyright)
  }
}

application {
  mainModule = 'de.muspellheim.activitysampling'
  mainClassName = 'de.muspellheim.activitysampling.App'
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      icon = 'src/main/macos/AppIcon.icns'
      imageName = 'Activity Sampling'
      installerType = 'dmg'
      imageOptions = [
        '--copyright', copyright,
        '--mac-sign',
        '--mac-signing-key-user-name', project.findProperty('macSigning.keyUserName')
      ]
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = 'src/main/win/app.ico'
      imageName = 'Activity Sampling'
      imageOptions = [
        '--copyright', copyright,
      ]
      installerName = 'Activity Sampling'
      installerType = 'msi'
      installerOptions = [
        '--copyright', "Copyright (c) ${year} Falko Schumann",
        '--license-file', '../LICENSE.txt',
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'Muspellheim',
        '--win-upgrade-uuid', 'cce3747c-06dc-466c-8c49-4aebe528da61'
      ]
    }
  }
}

gluonClient {
  resourcesList = ['app.properties']
  reflectionList = [
    'de.muspellheim.activitysampling.frontend.MainView',
    'de.muspellheim.activitysampling.frontend.PreferencesView',
    'de.muspellheim.activitysampling.frontend.InfoView',
    'javafx.geometry.Insets',
    'javafx.scene.control.Button',
    'javafx.scene.control.ChoiceBox',
    'javafx.scene.image.Image',
    'javafx.scene.image.ImageView',
    'javafx.scene.control.Label',
    'javafx.scene.control.MenuItem',
    'javafx.scene.control.ProgressBar',
    'javafx.scene.control.SplitMenuButton',
    'javafx.scene.control.TextArea',
    'javafx.scene.control.TextField',
    'javafx.scene.control.ToolBar',
    'javafx.scene.image.Image',
    'javafx.scene.image.ImageView',
    'javafx.scene.layout.VBox',
  ]
}
