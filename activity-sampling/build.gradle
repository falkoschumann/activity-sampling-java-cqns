import org.apache.tools.ant.taskdefs.condition.Os

import java.time.LocalDate

plugins {
  id 'activity-sampling.java-application-conventions'
  id 'activity-sampling.java-openjfx-conventions'
  id 'com.gluonhq.client-gradle-plugin'
}

dependencies {
  implementation project(':activity-sampling-frontend')
  implementation project(':activity-sampling-backend')
}

application {
  mainModule = 'de.muspellheim.activitysampling'
  mainClass = 'de.muspellheim.activitysampling.App'
}

def year = LocalDate.now().year
def copyright = "Copyright (c) ${year} Falko Schumann"

processResources {
  filesMatching('**/*.properties') {
    expand(version: rootProject.version, copyright: copyright)
  }
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      icon = 'src/main/macos/AppIcon.icns'
      imageName = 'Activity Sampling'
      installerType = 'dmg'
      imageOptions = [
        '--copyright', copyright,
        '--mac-sign',
        '--mac-signing-key-user-name', 'Falko Schumann (QC6EN37P56)'
      ]
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = 'src/main/win/app.ico'
      imageName = 'Activity Sampling'
      imageOptions = [
        '--copyright', copyright,
      ]
      installerName = 'Activity Sampling'
      installerType = 'msi'
      installerOptions = [
        '--copyright', copyright,
        '--license-file', '../LICENSE.txt',
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'Muspellheim',
        '--win-upgrade-uuid', 'cce3747c-06dc-466c-8c49-4aebe528da61'
      ]
    }
  }
}
